modeltype MDSDTEAM17 uses 'http://www.example.org/mdsdteam17';
modeltype PCM uses 'http://palladiosimulator.org/PalladioComponentModel/5.2';

transformation EnvironmentTransformation(in MDSD : MDSDTEAM17, out PCM);

main() {
	var asds = MDSD.objects()[MDSDTEAM17::environment::Environment];
 	asds->map env2PCM();
 	var repository = MDSD.objects()[MDSDTEAM17::repository::Repository];
 	repository->map repo2PCM();
}

//Allocation View
mapping MDSDTEAM17::allocation::Allocation::allocation2PCM() : PCM::allocation::Allocation {
	
}

mapping MDSDTEAM17::allocation::AllocationContext::allocContext2PCM() : PCM::allocation::AllocationContext {
	
}

//Environment View
mapping MDSDTEAM17::environment::Environment::env2PCM() : PCM::resourceenvironment::ResourceEnvironment {
	entityName := self.name;
	resourceContainer_ResourceEnvironment := self.containers.map container2PCM();
	linkingResources__ResourceEnvironment := self.containerLinks.map link2PCM();
}

mapping MDSDTEAM17::environment::Container::container2PCM() : PCM::resourceenvironment::ResourceContainer {
	entityName := self.name;
	id := self.name;
}

mapping MDSDTEAM17::environment::Link::link2PCM() : PCM::resourceenvironment::LinkingResource {
	entityName := self.name;
	id := self.name;
	connectedResourceContainers_LinkingResource := 
		self.containers.resolveIn(MDSDTEAM17::environment::Container::container2PCM, PCM::resourceenvironment::ResourceContainer);
	
	var o = object PCM::resourceenvironment::CommunicationLinkResourceSpecification {
		communicationLinkResourceType_CommunicationLinkResourceSpecification := new PCM::resourcetype::CommunicationLinkResourceType();
		latency_CommunicationLinkResourceSpecification := new PCM::core::PCMRandomVariable();
		throughput_CommunicationLinkResourceSpecification := new PCM::core::PCMRandomVariable();
	};
	communicationLinkResourceSpecifications_LinkingResource := o;
}

//Repository View
mapping MDSDTEAM17::repository::Repository::repo2PCM() : PCM::repository::Repository {
	id := self.name;
	dataTypes__Repository := MDSD.objects()[MDSDTEAM17::repository::Type]->map type2PCM();
	interfaces__Repository := self.interfaces->map interface2PCM();
	components__Repository := self.components->map comp2PCM();
}

mapping MDSDTEAM17::repository::Component::comp2PCM() : PCM::repository::RepositoryComponent {
	init {
		if (self.oclIsTypeOf(MDSDTEAM17::repository::Component)) {
			result := self.map basicComp2PCM();
		} else {
			result := self.oclAsType(MDSDTEAM17::assembly::CompositeComponent).map composite2PCM();
		}
	}
}

// Components
mapping MDSDTEAM17::repository::Component::basicComp2PCM() : PCM::repository::BasicComponent 
	when { self.oclIsTypeOf(MDSDTEAM17::repository::Component) }
{
	entityName := self.name;
	id := self.name;
	providedRoles_InterfaceProvidingEntity := self.providedInterfaces->map interface2ProvidedRole();
	requiredRoles_InterfaceRequiringEntity := self.requiredInterfaces->map interface2RequiredRole();
	
}

// Composite Components
mapping MDSDTEAM17::assembly::CompositeComponent::composite2PCM() : PCM::repository::CompositeComponent 
	when { self.oclIsTypeOf(MDSDTEAM17::assembly::CompositeComponent) }
{
	entityName := self.name;
	id := self.name;
	providedRoles_InterfaceProvidingEntity := self.providedInterfaces->map interface2ProvidedRole();
	requiredRoles_InterfaceRequiringEntity := self.requiredInterfaces->map interface2RequiredRole();
}

//Roles
mapping MDSDTEAM17::repository::Interface::interface2ProvidedRole() : PCM::repository::OperationProvidedRole {
	entityName := self.name;
	id := self.name + "_Role";
	providedInterface__OperationProvidedRole := self.resolveoneIn(MDSDTEAM17::repository::Interface::interface2PCM);
}

mapping MDSDTEAM17::repository::Interface::interface2RequiredRole() : PCM::repository::OperationRequiredRole {
	entityName := self.name;
	id := self.name + "_Role";
	requiredInterface__OperationRequiredRole := self.resolveoneIn(MDSDTEAM17::repository::Interface::interface2PCM);
}

//Interfaces, Signaturen und Parameter
mapping MDSDTEAM17::repository::Interface::interface2PCM() : PCM::repository::OperationInterface {
	id := "I" + self.name;
	signatures__OperationInterface := self.signatures->map signature2PCM();
	
}

mapping MDSDTEAM17::repository::Signature::signature2PCM() : PCM::repository::OperationSignature {
	id := self.name;
	returnType__OperationSignature := self.returnType.resolveoneIn(MDSDTEAM17::repository::Type::type2PCM, PCM::repository::DataType);
	parameters__OperationSignature := self.parameters->map parameter2PCM();
}

mapping MDSDTEAM17::repository::SignatureParameter::parameter2PCM() : PCM::repository::Parameter {
	parameterName := self.name;
	dataType__Parameter := self.type.resolveoneIn(MDSDTEAM17::repository::Type::type2PCM, PCM::repository::DataType);
}

//Datentypen
mapping MDSDTEAM17::repository::Type::type2PCM() : PCM::repository::CompositeDataType {
	id := self.name;
}
